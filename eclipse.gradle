apply plugin: 'eclipse'

eclipse.project.file.withXml { 
    addFilteredResources it 
    ignoreSvn it
    ignoreGradleOutputDir it
}

def addFilteredResources (projectRoot) {
    projectRoot.asNode().append new XmlParser().parseText('<filteredResources/>')
}

def addFilter (projectRoot, xml) {
    def filters = projectRoot.node.filteredResources.head()
    filters.append new XmlParser().parseText(xml)
}

def createFilter (filter, matchFor) {
    """
    <filter> 
        <id>${new Random().nextInt()}</id> 
        <name></name> 
        <type>${filter.type}</type> 
        <matcher> 
            <id>org.eclipse.ui.ide.multiFilter</id> 
            <arguments>1.0-${filter.matcher}-matches-false-false-${matchFor}</arguments> 
        </matcher> 
    </filter> 
"""
}

def ignoreSvn (projectRoot) {
    addFilter(projectRoot, 
        createFilter(type: 26, matcher: 'name', '.svn'))
}

def ignoreGradleOutputDir (projectRoot) {
    addFilter(projectRoot, 
        createFilter(type: 10, matcher: 'projectRelativePath', buildDir.name))
}
