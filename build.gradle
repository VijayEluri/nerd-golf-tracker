apply plugin: 'java'
defaultTasks 'clean', 'build'


sourceCompatibility = '1.6'
tasks.withType(Compile) { 
    options.compilerArgs << "-Xlint:-options" 
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


repositories {
    maven { url "http://localhost:8081/nexus/content/groups/public/" }
    mavenCentral()
}
configurations.all {
    resolutionStrategy {
        // cucumber-jvm 1.1.3 braucht scheinbar JUnit 4.11
        forcedModules = ['junit:junit:4.11']
    }
}


archivesBaseName = "nerd-golf-tracker"
jar.manifest {
    attributes('Main-Class': 'de.itagile.golf.Main')
}


sourceSets.test.java { srcDir 'src/unittests/java' }
dependencies {
    testCompile 'junit:junit' 
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-core:1.8.5'
}


sourceSets {
    cucumber {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}
dependencies {
    cucumberCompile 'info.cukes:cucumber-java:1.1.4'
    cucumberCompile 'info.cukes:cucumber-junit:1.1.4'
    cucumberCompile 'junit:junit' 
    cucumberCompile 'org.hamcrest:hamcrest-all:1.3'

    cucumberRuntime 'info.cukes:cucumber-picocontainer:1.1.4'
}
task cucumber() {
    dependsOn compileCucumberJava, jar
    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = sourceSets.cucumber.runtimeClasspath
            args = ['--strict', 
            	'-f', "html:${reporting.baseDir}/cucumber", 
            	'--tags', '~@in_arbeit',
            	'--glue', 'de.itagile.golf', 
            	'src/cucumber/resources']
        }
    }
}
cucumber.mustRunAfter test
check.dependsOn cucumber


apply from: 'eclipse.gradle'
apply from: 'idea.gradle'

