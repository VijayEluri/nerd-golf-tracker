
apply plugin: 'java'
defaultTasks 'clean', 'build'

def applyFrom (basenameWithoutExt) {
    apply from: "gradle/${basenameWithoutExt}.gradle"
}
applyFrom 'dslExtensions'


sourceCompatibility = '1.6'
tasks.withType(Compile) { 
    options.compilerArgs << "-Xlint:-options" 
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


repositories {
    maven { url "http://localhost:8081/nexus/content/groups/public/" }
    mavenCentral()
}
applyFrom 'dependencyResolution'


archivesBaseName = "nerd-golf-tracker"
jar.manifest {
    attributes('Main-Class': 'de.itagile.golf.Main')
}
applyFrom 'upload'


sourceSets.test.java { srcDir 'src/unittests/java' }
dependencies {
    testCompile junit, hamcrest
    testCompile 'org.mockito:mockito-core:1.8.5'
}

applyFrom 'cucumber'


applyFrom 'eclipse'
applyFrom 'idea'

