apply plugin: 'java'

defaultTasks 'clean', 'build', 'cucumber'

sourceSets.main.java { srcDir 'src' }
sourceSets.test {
    java {
        srcDir 'unittests'
        srcDir 'specification'
    }
    resources {
        srcDir 'specification'
    }
}

sourceCompatibility = '1.6'
tasks.withType(Compile) { 
    options.compilerArgs << "-Xlint:-options" 
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    maven { url "http://localhost:8081/nexus/content/groups/public/" }
    mavenCentral()
}
dependencies {
    testCompile 'org.jbehave:jbehave-core:2.5.9'
    testCompile 'junit:junit' 
    testCompile 'org.mockito:mockito-core:1.8.5'
}

archivesBaseName = "nerd-golf-tracker"
jar.manifest {
    attributes('Main-Class': 'de.itagile.golf.Main')
}

test {
    dependsOn 'jar'
    testLogging.showStandardStreams = true
}



sourceSets {
    cucumber {
        java.srcDir 'cucumber/java'
        resources.srcDir 'cucumber/resources'
    }
}

dependencies {
    cucumberCompile 'info.cukes:cucumber-java:1.1.4'
    cucumberCompile 'info.cukes:cucumber-junit:1.1.4'
    cucumberCompile 'junit:junit' 
}

configurations.all {
    resolutionStrategy {
        // cucumber-jvm 1.1.3 braucht scheinbar JUnit 4.11
        forcedModules = ['junit:junit:4.11']
    }
}

task cucumber() {
    dependsOn compileCucumberJava, jar
    doLast {
        javaexec {
            main = 'cucumber.api.cli.Main'
            classpath = configurations.cucumberRuntime + sourceSets.cucumber.output
            args = ['--strict', 
            	'-f', "html:${reporting.baseDir}/cucumber", 
            	'--tags', '~@in_arbeit',
            	'--glue', 'de.itagile.golf', 
            	'cucumber/resources']
        }
    }
}

apply from: 'eclipse.gradle'
apply from: 'idea.gradle'
