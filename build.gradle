apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

defaultTasks 'clean', 'build'

sourceSets.main.java { srcDir 'src' }
sourceSets.test {
    java {
        srcDir 'unittests'
        srcDir 'specification'
    }
    resources {
        srcDir 'specification'
    }
}
sourceCompatibility = '1.6'
tasks.withType(Compile) { 
    options.compilerArgs << "-Xlint:-options" 
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}
dependencies {
    testCompile 'org.jbehave:jbehave-core:2.5.9'
    testCompile 'junit:junit:4.8.1'
    testCompile 'org.mockito:mockito-core:1.8.5'
}


jar.manifest {
    attributes('Main-Class': 'de.itagile.golf.Main')
}


idea.project.ipr {
    withXml { provider ->
	disableSpellChecking provider.asNode()
    }
}

def disableSpellChecking (projectRoot) {
    def xmlThatDisablesSpellChecking = '''
  <component name="InspectionProjectProfileManager">
    <profiles>
      <profile version="1.0" is_locked="false">
        <option name="myName" value="Project Default" />
        <option name="myLocal" value="false" />
        <inspection_tool class="SpellCheckingInspection" enabled="false" level="TYPO" enabled_by_default="false">
          <option name="processCode" value="true" />
          <option name="processLiterals" value="true" />
          <option name="processComments" value="true" />
        </inspection_tool>
      </profile>
    </profiles>
    <option name="PROJECT_PROFILE" value="Project Default" />
    <option name="USE_PROJECT_PROFILE" value="true" />
    <version value="1.0" />
  </component>
'''
    projectRoot.append new XmlParser().parseText(xmlThatDisablesSpellChecking)
}

